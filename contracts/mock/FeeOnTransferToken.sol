// SPDX-License-Identifier: MIT

pragma solidity 0.6.6;

import "../lib/@openzeppelin/utils/Context.sol";
import "../lib/@openzeppelin/token/ERC20/IERC20.sol";
import "../lib/@openzeppelin/math/SafeMath.sol";
import "../lib/@openzeppelin/access/Ownable.sol";

// ERC20 that burns 1% on Transfers, faucet() added for testing
contract FeeOnTransferToken is Context, IERC20, Ownable {
    using SafeMath for uint256;

    mapping (address => uint256) private _balances;

    mapping (address => mapping (address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;
    uint8 private _decimals;
    uint8 private _burnRate;

    constructor (string memory name_, string memory symbol_, uint8 burnRate_) public {
        _name = name_;
        _symbol = symbol_;
        _decimals = 18;
        _burnRate = burnRate_;
    }

    function name() public view returns (string memory) {
        return _name;
    }

    function symbol() public override view returns (string memory) {
        return _symbol;
    }

    function decimals() public override view returns (uint8) {
        return _decimals;
    }

    function totalSupply() public view override returns (uint256) {
        return _totalSupply;
    }

    function balanceOf(address account) public view override returns (uint256) {
        return _balances[account];
    }

    function burnRate() public view returns (uint256) {
        return _burnRate;
    }

    function faucet() public {
        _mint(msg.sender, 100e18);
    }

    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amount, "ERC20: transfer amount exceeds allowance"));
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue, "ERC20: decreased allowance below zero"));
        return true;
    }

    function _transfer(address sender, address recipient, uint256 amount) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        uint256 toBurn = amount.mul(_burnRate).div(1000);
        _burn(sender, toBurn);
        _balances[sender] = _balances[sender].sub(amount.sub(toBurn), "ERC20: transfer amount exceeds balance");
        _balances[recipient] = _balances[recipient].add(amount.sub(toBurn));
        emit Transfer(sender, recipient, amount.sub(toBurn));
    }

    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply = _totalSupply.add(amount);
        _balances[account] = _balances[account].add(amount);
        emit Transfer(address(0), account, amount);
    }

    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        _balances[account] = _balances[account].sub(amount, "ERC20: burn amount exceeds balance");
        _totalSupply = _totalSupply.sub(amount);
        emit Transfer(account, address(0), amount);
    }

    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    function _setupDecimals(uint8 decimals_) internal {
        _decimals = decimals_;
    }

    function setBurnRate(uint8 burnRate_) external onlyOwner {
        require(burnRate_ != _burnRate, "BurnRate: No burn change");

        _burnRate = burnRate_;
    }

    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual { }
}